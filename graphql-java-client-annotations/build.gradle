plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.16"
    annotationProcessor "org.projectlombok:lombok:1.18.12"
    api group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.12.1"
    api group: "org.reactivestreams", name: "reactive-streams", version: "1.0.3"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        graphqlJavaClient(MavenPublication) {
            groupId = project.group
            artifactId = "graphql-java-client"
            version = project.version
            from components.java
            pom {
                name = "graphql-java-client"
                description = "GraphqL Java Client"
                url = "https://github.com/JacobMountain/graphql-java-client"
                scm {
                    url = "https://github.com/JacobMountain/graphql-java-client"
                    connection = "https://github.com/JacobMountain/graphql-java-client"
                    developerConnection = "https://github.com/JacobMountain/graphql-java-client"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/JacobMountain/graphql-java-client/blob/develop/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "JacobMountain"
                        name = "Jacob Mountain"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.env.NEXUS_USERNAME
                password = System.env.NEXUS_PASSWORD
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.graphqlJavaClient
}
// all publish tasks depend on the build task
tasks.withType(MavenPublication) {
    dependsOn build
}

